package loader;

import tools.NativeJS;
import loader.Global;

/**
 * –ë–∞–ª–∞–Ω—Å–∏—Ä–æ–≤—â–∏–∫ –Ω–∞–≥—Ä—É–∑–∫–∏. üö¶  
 * –ò—Å–ø–æ–ª—å–∑—É–µ—Ç—Å—è –¥–ª—è –æ–≥—Ä–∞–Ω–∏—á–µ–Ω–∏—è –∫–æ–ª–∏—á–µ—Å—Ç–≤–∞ –æ—Ç–ø—Ä–∞–≤–ª—è–µ–º—ã—Ö –∑–∞–ø—Ä–æ—Å–æ–≤
 * –≤ –µ–¥–∏–Ω–∏—Ü—É –≤—Ä–µ–º–µ–Ω–∏. –ó–∞–ø—Ä–æ—Å—ã –≤—Å—Ç–∞—é—Ç –≤ –æ—á–µ—Ä–µ–¥—å –∏ –æ—Ç–ø—Ä–∞–≤–ª—è—é—Ç—Å—è –ø–æ–∑–∂–µ,
 * –µ—Å–ª–∏ –∏—Ö –∫–æ–ª–∏—á–µ—Å—Ç–≤–æ –Ω–∞—á–∏–Ω–∞–µ—Ç –ø—Ä–µ–≤—ã—à–∞—Ç—å –∑–∞–¥–∞–Ω–Ω—ã–µ –ª–∏–º–∏—Ç—ã. –û–¥–∏–Ω
 * –±–∞–ª–∞–Ω—Å–∏—Ä–æ–≤—â–∏–∫ —Å–æ–æ—Ç–≤–µ—Ç—Å—Ç–≤—É–µ—Ç –æ–¥–Ω–æ–º—É —É–∑–ª—É.
 * 
 * –°–ø–æ—Å–æ–± –ø—Ä–∏–º–µ–Ω–µ–Ω–∏—è:
 * 1. –°–æ–∑–¥–∞—Ç—å —ç–∫–∑–µ–º–ø–ª—è—Ä `Balancer`, –Ω–∞—Å—Ç—Ä–æ–∏—Ç—å –Ω–µ–æ–±—Ö–æ–¥–∏–º—ã–π –ª–∏–º–∏—Ç –∑–∞–ø—Ä–æ—Å–æ–≤.
 * 2. –ù–∞–∑–Ω–∞—á–∏—Ç—å —Å–æ–∑–¥–∞–Ω–Ω—ã–π —ç–∫–∑–µ–º–ø–ª—è—Ä `Balancer` –∫–∞–∂–¥–æ–º—É –∑–∞–≥—Ä—É–∑—á–∏–∫—É `ILoader`
 *    **–¥–æ** –≤—ã–∑–æ–≤–∞ –º–µ—Ç–æ–¥–∞ `load()`.
 */
@:dce 
class Balancer
{
    /**
     * –ò–Ω—Ç–µ—Ä–≤–∞–ª –æ–±–Ω–æ–≤–ª–µ–Ω–∏–π –¥–ª—è –∑–∞–ø—É—Å–∫–∞ –∑–∞–ø—Ä–æ—Å–æ–≤. (mc)
     */
    static private inline var INTERVAL_UPDATE:Int = 100;

    // –ü—Ä–∏–≤–∞—Ç
    private var loaders:Array<ILoader> = new Array();
    private var interval:Dynamic = null;
    private var time:Float = 0;

    /**
     * –°–æ–∑–¥–∞—Ç—å –±–∞–ª–∞–Ω—Å–∏—Ä–æ–≤—â–∏–∫.
     * @param rps –û–≥—Ä–∞–Ω–∏—á–µ–Ω–∏–µ —á–∞—Å—Ç–æ—Ç—ã –∑–∞–ø—Ä–æ—Å–æ–≤. (Request per second)
     */
    public function new(rps:Float = 1) {
        this.rps = rps;
    }

    /**
     * –û–≥—Ä–∞–Ω–∏—á–µ–Ω–∏–µ —á–∞—Å—Ç–æ—Ç—ã –∑–∞–ø—Ä–æ—Å–æ–≤. (Request per second)
     * - –≠—Ç–æ –∑–Ω–∞—á–µ–Ω–∏–µ –Ω–µ –º–æ–∂–µ—Ç –±—ã—Ç—å –º–µ–Ω—å—à–µ `0`.
     * - –ï—Å–ª–∏ –∑–∞–¥–∞–Ω–æ `0` - –∑–∞–ø—Ä–æ—Å—ã –Ω–∏–∫–æ–≥–¥–∞ –Ω–µ –±—É–¥—É—Ç –æ—Ç–ø—Ä–∞–≤–ª–µ–Ω—ã.
     * 
     * –ü–æ —É–º–æ–ª—á–∞–Ω–∏—é: `1` (–û–¥–∏–Ω –∑–∞–ø—Ä–æ—Å –≤ —Å–µ–∫—É–Ω–¥—É)
     */
    public var rps(default, set):Float;
    function set_rps(value:Float):Float {
        if (value > 0) {
            if (value == rps)
                return value;

            rps = value;

            if (interval == null && length > 0)
                interval = Global.setInterval(onUpdate, INTERVAL_UPDATE, this);
        }
        else {
            if (rps == 0)
                return value;

            rps = 0;

            if (interval != null) {
                Global.clearInterval(interval);
                interval = null;
            }
        }

        return value;
    }

    /**
     * –ö–æ–ª–∏—á–µ—Å—Ç–≤–æ –∑–∞–ø—Ä–æ—Å–æ–≤ –≤ –æ—á–µ—Ä–µ–¥–∏. (–®—Ç—É–∫)  
     * –ü–æ —É–º–æ–ª—á–∞–Ω–∏—é: `0`
     */
    public var length(default, null):Int = 0;

    /**
     * –û–±–Ω–æ–≤–ª–µ–Ω–∏–µ.  
     * –ú–µ—Ç–æ–¥ —Å–¥–µ–ª–∞–Ω —Å—Ç–∞—Ç–∏—á–µ—Å–∫–∏–º, —á—Ç–æ –±—ã —Ö–∞–∫—Å –Ω–µ –∏—Å–ø–æ–ª—å–∑–æ–≤–∞–ª `bind()`,
     * –∫–æ—Ç–æ—Ä—ã–π –Ω—É–∂–µ–Ω –¥–ª—è –ø—Ä–∏–≤—è–∑–∫–∏ –∫–æ–Ω—Ç–µ–Ω–∫—Å—Ç–∞ `this`. –¢–∞–∫ –±—É–¥–µ—Ç —Ä–∞–±–æ—Ç–∞—Ç—å
     * –±—ã—Å—Ç—Ä–µ–µ.
     * @param b –û–±–Ω–æ–≤–ª—è–µ–º—ã–π –±–∞–ª–∞–Ω—Å–∏—Ä–æ–≤—â–∏–∫.
     */
    static private function onUpdate(b:Balancer):Void {

        // –û–±–Ω–æ–≤–ª–µ–Ω–∏–µ
        // –í—ã–∫–ª—é—á–∞–µ–º –æ–±–Ω–æ–≤–ª–µ–Ω–∏–µ, –µ—Å–ª–∏ –∑–∞–≥—Ä—É–∑—á–∏–∫–æ–≤ –±–æ–ª—å—à–µ –Ω–µ—Ç:
        if (b.length == 0) {
            if (b.interval != null) {
                Global.clearInterval(b.interval);
                b.interval = null;
            }
            return;
        }

        // –ú–µ—Ç–æ–¥ –≥–∞—Ä–∞–Ω—Ç–∏—Ä–æ–≤–∞–Ω–Ω–æ –Ω–µ –≤—ã–∑–æ–≤–µ—Ç—Å—è, –µ—Å–ª–∏ rps <= 0:
        var t = 1000 / b.rps;         // –í—Ä–µ–º—è –Ω–∞ 1 –∑–∞–ø—Ä–æ—Å. (mc)
        var ct = NativeJS.now();      // –¢–µ–∫—É—â–µ–µ –≤—Ä–µ–º—è. (mc)
        var dt = ct - b.time;         // –†–µ–∞–ª—å–Ω–æ –ø—Ä–æ—à–µ–¥—à–µ–µ –≤—Ä–µ–º—è —Å –º–æ–º–µ–Ω—Ç–∞ –ø–æ—Å–ª–µ–¥–Ω–µ–≥–æ –∑–∞–ø—Ä–æ—Å–∞. (mc)

        // –í—Ä–µ–º–µ–Ω–∏ –ø—Ä–æ—à–ª–æ —Å–ª–∏—à–∫–æ–º –º–∞–ª–æ –¥–∞–∂–µ –¥–ª—è –≤—ã–ø–æ–ª–Ω–µ–Ω–∏—è 1 –∑–∞–ø—Ä–æ—Å–∞:
        if (dt < t)
            return;

        // –ú–∞–∫—Å–∏–º–∞–ª—å–Ω–æ –ø—Ä–æ—à–µ–¥—à–µ–µ –≤—Ä–µ–º—è –¥–ª—è 1 —Ç–∏–∫–∞: (–ß—Ç–æ–± –Ω–µ –±—ã–ª–æ —Å–∫–∞—á–∫–∞ –∑–∞–ø—Ä–æ—Å–æ–≤)
        var mt = Math.max(t + INTERVAL_UPDATE, INTERVAL_UPDATE * 2);
        var num = 0; // <-- –ö–æ–ª–∏—á–µ—Å—Ç–≤–æ –æ—Ç–ø—Ä–∞–≤–ª—è–µ–º—ã—Ö –∑–∞–ø—Ä–æ—Å–æ–≤
        if (dt > mt) {
            // –°–∫–∞—á–æ–∫ –ø—Ä–æ—à–µ–¥—à–µ–≥–æ –≤—Ä–µ–º–µ–Ω–∏:
            num = Math.floor(mt / t);
            if (num > b.length)
                num = b.length;
            
            b.time = ct; // <-- –ü—Ä–∏–±–ª–∏–∂—ë–Ω–Ω–æ–µ —É—Ä–∞–≤–Ω–µ–Ω–∏–µ —Å –ø—Ä–æ–ø—É—Å–∫–æ–º –ø—Ä–æ–º–µ–∂—É—Ç–∫–∞ –≤ —Å–∫–∞—á–∫–µ
        }
        else {
            // –ù–µ—Ç —Å–∫–∞—á–∫–∞ –ø—Ä–æ—à–µ–¥—à–µ–≥–æ –≤—Ä–µ–º–µ–Ω–∏:
            num = Math.floor(dt / t);
            if (num > b.length)
                num = b.length;
            
            b.time += num * t; // <-- –¢–æ—á–Ω–æ–µ —É—Ä–∞–≤–Ω–µ–Ω–∏–µ, –Ω–∏ —Å–µ–∫—É–Ω–¥—ã –ø—Ä–æ–ø—É—â–µ–Ω–æ
        }

        // –ß–∏—Å—Ç–∏–º –æ—á–µ—Ä–µ–¥—å –æ—Ç null –∏ —Å–æ—Ä—Ç–∏—Ä—É–µ–º –µ—ë:
        var len = b.loaders.length;
        var i = 0;
        var j = 0;
        while (i < len) {
            if (b.loaders[i] == null) {
                i ++;
                continue;
            }

            b.loaders[j] = b.loaders[i];
            i ++;
            j ++;
        }
        if (i != j)
            b.loaders.resize(j);
        b.loaders.sort(compare);

        // –ó–∞–±–∏—Ä–∞–µ–º –∏–∑ —Å–ø–∏—Å–∫–∞ –æ—Ç–ø—Ä–∞–≤–ª—è–µ–º—ã–µ —ç–ª–µ–º–µ–Ω—Ç—ã: (–ü—Ä–∏ –æ—Ç–ø—Ä–∞–≤–∫–µ —Å–ø–∏—Å–æ–∫ –º–æ–∂–µ—Ç –∏–∑–º–µ–Ω–∏—Ç—å—Å—è!)
        var arr:Array<ILoader> = NativeJS.array(num);
        i = num;
        while (i-- > 0) {
            arr[i] = b.loaders[i];
            b.loaders[i] = null; // <-- –£–¥–∞–ª–∏—Ç—Å—è –Ω–∞ —Å–ª–µ–¥—É—é—â–µ–π –∏—Ç—Ç–µ—Ä–∞—Ü–∏–∏
        }
        b.length -= num;

        // –ë–µ–∑–æ–ø–∞—Å–Ω–æ –∏–Ω–∏—Ü–∏–∏—Ä—É–µ–º –∑–∞–ø—Ä–æ—Å—ã:
        while (num-- > 0) {
            if (arr[num].state == LoaderState.PENDING && arr[num].balancer == b)
                arr[num].loadStart();
        }
    }

    static private function compare(x:ILoader, y:ILoader):Int {
        if (x.priority > y.priority)
            return -1;
        if (x.priority < y.priority)
            return 1;
        
        return 0;
    }

    /**
     * –í—ã–ø–æ–ª–Ω–∏—Ç—å –≤—Å–µ –∑–∞–ø—Ä–æ—Å—ã –≤ –æ—á–µ—Ä–µ–¥–∏.  
     * –í—ã–∑–æ–≤ —ç—Ç–æ–≥–æ –º–µ—Ç–æ–¥–∞ –ø—Ä–∏–≤–æ–¥–∏—Ç –∫ –º–≥–Ω–æ–≤–µ–Ω–Ω–æ–π –æ—Ç–ø—Ä–∞–≤–∫–µ –≤—Å–µ—Ö –∑–∞–ø—Ä–æ—Å–æ–≤,
     * –Ω–∞—Ö–æ–¥—è—â–∏—Ö—Å—è –≤ –¥–∞–Ω–Ω—ã–π –º–æ–º–µ–Ω—Ç –≤ –æ—á–µ—Ä–µ–¥–∏.  
     * –û—á–µ—Ä–µ–¥—å –∑–∞–ø—Ä–æ—Å–æ–≤ –æ—á–∏—â–∞–µ—Ç—Å—è.
     */
    public function flush():Void {
        var i = 0;
        var arr = loaders;
        var len = arr.length;

        loaders = new Array(); // <-- –°–ø–∏—Å–æ–∫ –º–æ–∂–µ—Ç –∏–∑–º–µ–Ω–∏—Ç—å—Å—è –∏–∑-–∑–∞ –≤—ã–∑–æ–≤–∞ –∫–æ–ª–±–µ–∫–æ–≤
        time = NativeJS.now();
        length = 0;

        // –û–±–Ω–æ–≤–ª–µ–Ω–∏—è –Ω–µ –Ω—É–∂–Ω—ã:
        if (interval != null) {
            Global.clearInterval(interval);
            interval = null;
        }

        // –ë–µ–∑–æ–ø–∞—Å–Ω–æ –∏–Ω–∏—Ü–∏–∏—Ä—É–µ–º –∑–∞–ø—Ä–æ—Å—ã:
        while (i < len) {
            var l = arr[i++];
            if (l != null && l.state == LoaderState.PENDING && l.balancer == this)
                l.loadStart();
        }
    }

    /**
     * –û—á–∏—Å—Ç–∏—Ç—å –±–∞–ª–∞–Ω—Å–∏—Ä–æ–≤—â–∏–∫.
     * - –£ –∑–∞–≥—Ä—É–∑—á–∏–∫–æ–≤ –≤ –æ—á–µ—Ä–µ–¥–∏ –≤—ã–∑—ã–≤–∞–µ—Ç—Å—è –º–µ—Ç–æ–¥: `Loader.close()`. (–°–æ—Å—Ç–æ—è–Ω–∏–µ: `LoaderState.PENDING`)
     * - –£–∂–µ –≤—ã–ø–æ–ª–Ω—è–µ–º—ã–µ –∏ –∑–∞–≤–µ—Ä—à—ë–Ω–Ω—ã–µ –∑–∞–ø—Ä–æ—Å—ã - –∏–≥–Ω–æ—Ä–∏—Ä—É—é—Ç—Å—è.
     * - –û—á–µ—Ä–µ–¥—å –∑–∞–ø—Ä–æ—Å–æ–≤ –æ—á–∏—â–∞–µ—Ç—Å—è.
     */
    public function clear():Void {
        var i = 0;
        var arr = loaders;
        var len = arr.length;
        
        loaders = new Array(); // <-- –°–ø–∏—Å–æ–∫ –º–æ–∂–µ—Ç –∏–∑–º–µ–Ω–∏—Ç—å—Å—è –∏–∑-–∑–∞ –≤—ã–∑–æ–≤–∞ –∫–æ–ª–±–µ–∫–æ–≤
        length = 0;

        // –û–±–Ω–æ–≤–ª–µ–Ω–∏—è –Ω–µ –Ω—É–∂–Ω—ã:
        if (interval != null) {
            Global.clearInterval(interval);
            interval = null;
        }

        // –ë–µ–∑–æ–ø–∞—Å–Ω–æ –∑–∞–∫—Ä—ã–≤–∞–µ–º –æ—á–µ—Ä–µ–¥—å:
        while (i < len) {
            var l = arr[i++];
            if (l == null || l.state != LoaderState.PENDING)
                continue;

            l.close();
        }
    }

    /**
     * –î–æ–±–∞–≤–∏—Ç—å –∑–∞–≥—Ä—É–∑—á–∏–∫ –≤ –æ—á–µ—Ä–µ–¥—å –Ω–∞ –æ—Ç–ø—Ä–∞–≤–∫—É.  
     * –†–µ–≥–∏—Å—Ç—Ä–∏—Ä—É–µ—Ç –ø–µ—Ä–µ–¥–∞–Ω–Ω—ã–π –∑–∞–≥—Ä—É–∑—á–∏–∫ –≤ –æ—á–µ—Ä–µ–¥–∏ –∏ –∏–Ω–∏—Ü–∏–∏—Ä—É–µ—Ç
     * –≤ –±—É–¥—É—â–µ–º –µ–≥–æ –∑–∞–≥—Ä—É–∑–∫—É. –≠—Ç–æ—Ç –º–µ—Ç–æ–¥ –Ω–µ –≤—ã–ø–æ–ª–Ω—è–µ—Ç –Ω–∏–∫–∞–∫–∏—Ö
     * –ø—Ä–æ–≤–µ—Ä–æ–∫ –∏ –ø—Ä–æ—Å—Ç–æ –¥–æ–±–∞–≤–ª—è–µ—Ç –ø–µ—Ä–µ–¥–∞–Ω–Ω—ã–π —ç–∫–∑–µ–º–ø–ª—è—Ä –≤ –æ—á–µ—Ä–µ–¥—å.
     * - –≠—Ç–æ—Ç –º–µ—Ç–æ–¥ –¥–æ–ª–∂–µ–Ω –≤—ã–∑—ã–≤–∞—Ç—å—Å—è —Ç–æ–ª—å–∫–æ –æ–¥–∏–Ω —Ä–∞–∑, –∏–Ω–∞—á–µ –±—É–¥—É—Ç
     *   –¥—É–±–ª–∏ –≤ –æ—á–µ—Ä–µ–¥–∏.
     * - –ù–µ –∑–∞–±—É–¥—å—Ç–µ —É–¥–∞–ª–∏—Ç—å –∑–∞–≥—Ä—É–∑—á–∏–∫ –∏–∑ –æ—á–µ—Ä–µ–¥–∏, –µ—Å–ª–∏ –µ–≥–æ –∑–∞–≥—Ä—É–∑–∫—É
     *   –æ—Ç–º–µ–Ω–∏—Ç –ø–æ–ª—å–∑–æ–≤–∞—Ç–µ–ª—å –≤—ã–∑–æ–≤–æ–º –º–µ—Ç–æ–¥–∞: `Loader.close()`.
     * @param loader –ó–∞–≥—Ä—É–∑—á–∏–∫.
     */
    @:allow(loader.ILoader)
    private function add(loader:ILoader):Void {
        length ++;
        loaders.push(loader);
        
        if (interval == null && rps > 0)
            interval = Global.setInterval(onUpdate, INTERVAL_UPDATE, this);
    }

    /**
     * –£–¥–∞–ª–∏—Ç—å –∑–∞–≥—Ä—É–∑—á–∏–∫ –∏–∑ –æ—á–µ—Ä–µ–¥–∏ –Ω–∞ –æ—Ç–ø—Ä–∞–≤–∫—É.  
     * –£–¥–∞–ª—è–µ—Ç –∏–∑ –æ—á–µ—Ä–µ–¥–∏ –ø–µ—Ä–≤—ã–π –Ω–∞–π–¥–µ–Ω–Ω—ã–π —ç–∫–∑–µ–º–ø–ª—è—Ä.
     * –≠—Ç–æ—Ç –º–µ—Ç–æ–¥ –Ω–µ –≤—ã–ø–æ–ª–Ω—è–µ—Ç –Ω–∏–∫–∞–∫–∏—Ö –¥–æ–ø–æ–ª–Ω–∏—Ç–µ–ª—å–Ω—ã—Ö –¥–µ–π—Å—Ç–≤–∏–π.
     * @param loader –ó–∞–≥—Ä—É–∑—á–∏–∫.
     */
    @:allow(loader.ILoader)
    private function remove(loader:ILoader):Void {
        var i = 0;
        var len = loaders.length;
        while (i < len) {
            if (loaders[i] == loader) {
                length --;
                loaders[i] = null; // <-- –°–ø–∏—Å–æ–∫ –ø–µ—Ä–µ—Å—Ç—Ä–æ–∏—Ç—Å—è –ø—Ä–∏ —Å–ª–µ–¥—É—é—â–µ–º –æ–±–Ω–æ–≤–ª–µ–Ω–∏–∏.
                return;
            }

            i ++;
        }
    }
}